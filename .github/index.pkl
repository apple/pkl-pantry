//===----------------------------------------------------------------------===//
// Copyright Â© 2025 Apple Inc. and the Pkl project authors. All rights reserved.
//
// Licensed under the Apache License, Version 2.0 (the "License");
// you may not use this file except in compliance with the License.
// You may obtain a copy of the License at
//
//     https://www.apache.org/licenses/LICENSE-2.0
//
// Unless required by applicable law or agreed to in writing, software
// distributed under the License is distributed on an "AS IS" BASIS,
// WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
// See the License for the specific language governing permissions and
// limitations under the License.
//===----------------------------------------------------------------------===//
amends "@pkl.impl.ghactions/PklCI.pkl"

import "@com.github.actions/catalog.pkl"
import "@com.github.actions/context.pkl"
import "@com.github.actions/Workflow.pkl"
import "@pkl.impl.ghactions/steps/SetupPkl.pkl"

triggerPackageDocsBuild = "main"

testReports {
  junit {
    "build/test-results/**/*.xml"
  }
}

local buildAndTest: Workflow = new {
  jobs {
    ["build"] {
      `runs-on` = "ubuntu-latest"
      steps {
        (catalog.`actions/checkout@v6`) {
          with {
            `fetch-depth` = 0
          }
        }
        (catalog.`actions/setup-java@v5`) {
          with {
            `java-version` = "21"
            distribution = "temurin"
            cache = "gradle"
          }
        }
        // pkl cli used for ../packages/pkl.tar/examples/test_examples.sh
        new SetupPkl { version = "0.30.0" }.step
        new {
          run = "./gradlew build"
        }
        (catalog.`actions/upload-artifact@v5`) {
          with {
            name = "build"
            path = "build"
          }
        }
      }
    }
  }
}

prb = buildAndTest

build = buildAndTest

main = (buildAndTest) {
  jobs {
    ["release"] {
      `runs-on` = "ubuntu-latest"
      permissions {
        contents = "write"
      }
      steps {
        (catalog.`actions/checkout@v6`) {
          with {
            `fetch-depth` = 0
          }
        }
        (catalog.`actions/download-artifact@v6`) {
          with {
            name = "build"
            path = "build"
          }
        }
        new {
          name = "Publish release on GitHub"
          env {
            ["GH_TOKEN"] = context.github.token
            ["GH_REPO"] = context.github.repository
            ["SHA"] = context.github.sha
          }
          // language=bash
          run =
            #"""
            if [[ -d build/releases && -n "$(ls -A build/releases)" ]]
            then
              for dir in build/releases/*
              do
                if [[ -d "$dir" ]]
                then
                  pkg=$(basename "$dir")
                  if gh release view "$pkg" ; then
                    echo "Package $pkg already published"
                  else
                    # TODO we can be kinder to GitHub by querying once for all releases.
                    echo -n "> Releasing $pkg at SHA1 "$SHA"..."
                    gh release create "$pkg" \
                      --title "$pkg" \
                      --target "$SHA" \
                      --notes "Release of $pkg" \
                      "$dir"/*
                    echo "DONE"
                  fi
                else
                  echo "> SKIPPING $dir; not a directory"
                fi
              done
            else
              echo "No new packages to release."
            fi
            """#
        }
      }
      needs {
        "build"
      }
    }
  }
}
