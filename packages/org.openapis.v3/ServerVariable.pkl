//===----------------------------------------------------------------------===//
// Copyright Â© 2024 Apple Inc. and the Pkl project authors. All rights reserved.
//
// Licensed under the Apache License, Version 2.0 (the "License");
// you may not use this file except in compliance with the License.
// You may obtain a copy of the License at
//
//     https://www.apache.org/licenses/LICENSE-2.0
//
// Unless required by applicable law or agreed to in writing, software
// distributed under the License is distributed on an "AS IS" BASIS,
// WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
// See the License for the specific language governing permissions and
// limitations under the License.
//===----------------------------------------------------------------------===//
/// An object representing a Server Variable for server URL template
/// substitution.
module org.openapis.v3.ServerVariable

/// An enumeration of string values to be used if the substitution options are from a limited set.
///
/// The array SHOULD NOT be empty.
enum: Listing<String>(!isEmpty, isDistinct)?

/// The default value to use for substitution, which SHALL be sent if an alternate value is not supplied.
///
/// Note this behavior is different than the Schema Object's treatment of default values, because in those cases
/// parameter values are optional. If the enum is defined, the value SHOULD exist in the enum's values.
default: String(enum == null || enum.toList().contains(this))

/// An optional description for the server variable.
///
/// CommonMark syntax MAY be used for rich text representation.
@SourceCode { language = "Markdown" }
description: String?
