//===----------------------------------------------------------------------===//
// Copyright Â© 2024-2025 Apple Inc. and the Pkl project authors. All rights reserved.
//
// Licensed under the Apache License, Version 2.0 (the "License");
// you may not use this file except in compliance with the License.
// You may obtain a copy of the License at
//
//     https://www.apache.org/licenses/LICENSE-2.0
//
// Unless required by applicable law or agreed to in writing, software
// distributed under the License is distributed on an "AS IS" BASIS,
// WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
// See the License for the specific language governing permissions and
// limitations under the License.
//===----------------------------------------------------------------------===//
module k8s.contrib.crd.tests.ModuleGenerator

amends "pkl:test"

import "@k8s/api/core/v1/ResourceRequirements.pkl"
import "@k8s/api/core/v1/EnvVar.pkl"
import "@k8s/api/networking/v1/NetworkPolicy.pkl"

import "../generate.pkl"

local generator = (generate) {
  sourceContents = read("fixtures/crds.yaml")
  source = "dummy://test_uri"
  converters {
    ["restateclusters.restate.dev"] {
      [List("spec", "compute", "env", "[]")] = EnvVar
      [List("spec", "compute", "resources")] = ResourceRequirements
      [List("spec", "security", "networkPeers", "ingress", "[]")] = NetworkPolicy.NetworkPolicyPeer
      [List("spec", "security", "networkPeers", "admin", "[]")] = NetworkPolicy.NetworkPolicyPeer
      [List("spec", "security", "networkPeers", "metrics", "[]")] = NetworkPolicy.NetworkPolicyPeer
    }
  }
}

local generator2 = (generator) {
  k8sImportPath = "@k8s"
}

local generator3 = (generator) {
  k8sImportPath = "package://pkg.pkl-lang.org/pkl-k8s/k8s@1.1.0"
}

examples {
  for (filename, value in generator.output.files!!) {
    [filename] {
      value.text
    }
  }

  for (filename, value in generator2.output.files!!) {
    ["\(filename) -- dependency notation for k8s"] {
      value.text
    }
  }

  for (filename, value in generator3.output.files!!) {
    ["\(filename) -- different version of k8s"] {
      value.text
    }
  }

  ["conflicting schemas"] {
    for (_, value in (generate) {
      sourceContents = read("fixtures/crds_conflict.yaml")
      source = "dummy://test_uri"
    }.output.files!!) {
      value.text
    }
  }
}
