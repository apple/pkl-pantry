//===----------------------------------------------------------------------===//
// Copyright Â© 2024-2025 Apple Inc. and the Pkl project authors. All rights reserved.
//
// Licensed under the Apache License, Version 2.0 (the "License");
// you may not use this file except in compliance with the License.
// You may obtain a copy of the License at
//
//     https://www.apache.org/licenses/LICENSE-2.0
//
// Unless required by applicable law or agreed to in writing, software
// distributed under the License is distributed on an "AS IS" BASIS,
// WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
// See the License for the specific language governing permissions and
// limitations under the License.
//===----------------------------------------------------------------------===//
/// The action metadata, to be placed as `action.yml` in a repository.
///
/// NOTE: To use GitHub Actions in a repo (e.g. what goes inside `.github/workflows`), define a _workflow_
/// instead.
/// For defining workflows, see `Workflow.pkl` in this package.
///
/// Documentation: <https://docs.github.com/en/actions/reference/workflows-and-actions/metadata-syntax>
module com.github.actions.Action

/// The name of your action.
///
/// GitHub displays the `name` in the Actions tab to help visually identify actions in each job.
name: String

/// The name of the action's author.
author: String?

/// A short description of the action.
description: String

/// Input parameters allow you to specify data that the action expects to use during runtime.
///
/// GitHub stores input parameters as environment variables.
/// Input ids with uppercase letters are converted to lowercase during runtime.
/// We recommended using lowercase input ids.
inputs: Mapping<String(matches(Regex("^[_a-zA-Z][a-zA-Z0-9_-]*$"))), Input>?

/// Output parameters allow you to declare data that an action sets.
///
/// Actions that run later in a workflow can use the output data set in previously run actions.
/// For example, if you had an action that performed the addition of two inputs (x + y = z), the
/// action could output the sum (z) for other actions to use as an input.
/// Outputs can be a maximum of 1 MB per job.
/// The total of all outputs in a workflow run can be a maximum of 50 MB.
/// Size is approximated based on UTF-16 encoding.
/// If you don't declare an output in your action metadata file, you can still set outputs and use
/// them in a workflow.
/// For more information on setting outputs in an action, see
/// [Workflow commands for GitHub Actions](https://docs.github.com/en/actions/reference/workflows-and-actions/workflow-commands#setting-an-output-parameter).
outputs: Mapping<
  String(matches(Regex("^[_a-zA-Z][a-zA-Z0-9_-]*$"))),
  Output((value != null).implies(runs is RunsComposite))
>?

/// Specifies whether this is a JavaScript action, a composite action, or a Docker container action
/// and how the action is executed.
runs: RunsJavascript | RunsComposite | RunsDocker

/// You can use a color and Feather icon to create a badge to personalize and distinguish your
/// action.
///
/// Badges are shown next to your action name in GitHub Marketplace.
branding: Branding?

/// A string identifier to associate with the input.
///
/// The value of `<input_id>` is a map of the input's metadata.
/// The `<input_id>` must be a unique identifier within the inputs object.
/// The `<input_id>` must start with a letter or `_` and contain only alphanumeric characters, `-`,
/// or `_`.
class Input {
  /// A string description of the input parameter.
  description: String

  /// A string shown to users using the deprecated input.
  deprecationMessage: String?

  /// A boolean to indicate whether the action requires the input parameter.
  ///
  /// Set to `true` when the parameter is required.
  required: Boolean?

  /// The default value that is used when an input parameter isn't specified in a workflow file.
  default: (String | Boolean | Int)?
}

class Output {
  /// A string description of the output parameter.
  description: String

  /// The value that the output parameter will be mapped to.
  ///
  /// You can set this to a string or an expression with context.
  /// For example, you can use the steps context to set the value of an output to the output value
  /// of a step.
  value: String?
}

/// You can use a color and Feather icon to create a badge to personalize and distinguish your
/// action.
///
/// Badges are shown next to your action name in GitHub Marketplace.
class Branding {
  /// The background color of the badge.
  color: (
    "white" | "black" | "yellow" | "blue" | "green" | "orange" | "red" | "purple" | "gray-dark"
  )?

  /// The name of the Feather icon to use.
  icon: (
    "activity"
      | "airplay"
      | "alert-circle"
      | "alert-octagon"
      | "alert-triangle"
      | "align-center"
      | "align-justify"
      | "align-left"
      | "align-right"
      | "anchor"
      | "aperture"
      | "archive"
      | "arrow-down-circle"
      | "arrow-down-left"
      | "arrow-down-right"
      | "arrow-down"
      | "arrow-left-circle"
      | "arrow-left"
      | "arrow-right-circle"
      | "arrow-right"
      | "arrow-up-circle"
      | "arrow-up-left"
      | "arrow-up-right"
      | "arrow-up"
      | "at-sign"
      | "award"
      | "bar-chart-2"
      | "bar-chart"
      | "battery-charging"
      | "battery"
      | "bell-off"
      | "bell"
      | "bluetooth"
      | "bold"
      | "book-open"
      | "book"
      | "bookmark"
      | "box"
      | "briefcase"
      | "calendar"
      | "camera-off"
      | "camera"
      | "cast"
      | "check-circle"
      | "check-square"
      | "check"
      | "chevron-down"
      | "chevron-left"
      | "chevron-right"
      | "chevron-up"
      | "chevrons-down"
      | "chevrons-left"
      | "chevrons-right"
      | "chevrons-up"
      | "circle"
      | "clipboard"
      | "clock"
      | "cloud-drizzle"
      | "cloud-lightning"
      | "cloud-off"
      | "cloud-rain"
      | "cloud-snow"
      | "cloud"
      | "code"
      | "command"
      | "compass"
      | "copy"
      | "corner-down-left"
      | "corner-down-right"
      | "corner-left-down"
      | "corner-left-up"
      | "corner-right-down"
      | "corner-right-up"
      | "corner-up-left"
      | "corner-up-right"
      | "cpu"
      | "credit-card"
      | "crop"
      | "crosshair"
      | "database"
      | "delete"
      | "disc"
      | "dollar-sign"
      | "download-cloud"
      | "download"
      | "droplet"
      | "edit-2"
      | "edit-3"
      | "edit"
      | "external-link"
      | "eye-off"
      | "eye"
      | "fast-forward"
      | "feather"
      | "file-minus"
      | "file-plus"
      | "file-text"
      | "file"
      | "film"
      | "filter"
      | "flag"
      | "folder-minus"
      | "folder-plus"
      | "folder"
      | "gift"
      | "git-branch"
      | "git-commit"
      | "git-merge"
      | "git-pull-request"
      | "globe"
      | "grid"
      | "hard-drive"
      | "hash"
      | "headphones"
      | "heart"
      | "help-circle"
      | "home"
      | "image"
      | "inbox"
      | "info"
      | "italic"
      | "layers"
      | "layout"
      | "life-buoy"
      | "link-2"
      | "link"
      | "list"
      | "loader"
      | "lock"
      | "log-in"
      | "log-out"
      | "mail"
      | "map-pin"
      | "map"
      | "maximize-2"
      | "maximize"
      | "menu"
      | "message-circle"
      | "message-square"
      | "mic-off"
      | "mic"
      | "minimize-2"
      | "minimize"
      | "minus-circle"
      | "minus-square"
      | "minus"
      | "monitor"
      | "moon"
      | "more-horizontal"
      | "more-vertical"
      | "move"
      | "music"
      | "navigation-2"
      | "navigation"
      | "octagon"
      | "package"
      | "paperclip"
      | "pause-circle"
      | "pause"
      | "percent"
      | "phone-call"
      | "phone-forwarded"
      | "phone-incoming"
      | "phone-missed"
      | "phone-off"
      | "phone-outgoing"
      | "phone"
      | "pie-chart"
      | "play-circle"
      | "play"
      | "plus-circle"
      | "plus-square"
      | "plus"
      | "pocket"
      | "power"
      | "printer"
      | "radio"
      | "refresh-ccw"
      | "refresh-cw"
      | "repeat"
      | "rewind"
      | "rotate-ccw"
      | "rotate-cw"
      | "rss"
      | "save"
      | "scissors"
      | "search"
      | "send"
      | "server"
      | "settings"
      | "share-2"
      | "share"
      | "shield-off"
      | "shield"
      | "shopping-bag"
      | "shopping-cart"
      | "shuffle"
      | "sidebar"
      | "skip-back"
      | "skip-forward"
      | "slash"
      | "sliders"
      | "smartphone"
      | "speaker"
      | "square"
      | "star"
      | "stop-circle"
      | "sun"
      | "sunrise"
      | "sunset"
      | "table"
      | "tablet"
      | "tag"
      | "target"
      | "terminal"
      | "thermometer"
      | "thumbs-down"
      | "thumbs-up"
      | "toggle-left"
      | "toggle-right"
      | "trash-2"
      | "trash"
      | "trending-down"
      | "trending-up"
      | "triangle"
      | "truck"
      | "tv"
      | "type"
      | "umbrella"
      | "underline"
      | "unlock"
      | "upload-cloud"
      | "upload"
      | "user-check"
      | "user-minus"
      | "user-plus"
      | "user-x"
      | "user"
      | "users"
      | "video-off"
      | "video"
      | "voicemail"
      | "volume-1"
      | "volume-2"
      | "volume-x"
      | "volume"
      | "watch"
      | "wifi-off"
      | "wifi"
      | "wind"
      | "x-circle"
      | "x-square"
      | "x"
      | "zap-off"
      | "zap"
      | "zoom-in"
      | "zoom-out"
      | String
  )?
}

/// Configures the path to the action's code and the application used to execute the code.
class RunsJavascript {
  /// The application used to execute the code specified in `main`.
  using: "node12" | "node16" | "node20" | "node24"

  /// The file that contains your action code. The application specified in `using` executes this file.
  main: String

  /// Allows you to run a script at the start of a job, before the `main:` action begins.
  ///
  /// For example, you can use `pre:` to run a prerequisite setup script.
  /// The application specified with the `using` syntax will execute this file.
  /// The `pre:` action always runs by default but you can override this using `pre-if`.
  pre: String?

  /// Allows you to define conditions for the `pre:` action execution.
  ///
  /// The `pre:` action will only run if the conditions in `pre-if` are met.
  /// If not set, then `pre-if` defaults to `always()`.
  /// Note that the `step` context is unavailable, as no steps have run yet.
  @SourceCode { language = "GithubExpressionLanguage" }
  `pre-if`: String?

  /// Allows you to run a script at the end of a job, once the `main:` action has completed.
  ///
  /// For example, you can use `post:` to terminate certain processes or remove unneeded files.
  /// The application specified with the `using` syntax will execute this file.
  /// The `post:` action always runs by default but you can override this using `post-if`.
  post: String?

  /// Allows you to define conditions for the `post:` action execution.
  ///
  /// The `post:` action will only run if the conditions in `post-if` are met.
  /// If not set, then `post-if` defaults to `always()`.
  @SourceCode { language = "GithubExpressionLanguage" }
  `post-if`: String?
}

/// Configures the path to the composite action, and the application used to execute the code.
class RunsComposite {
  /// To use a composite run steps action, set this to 'composite'.
  using: "composite"

  /// The run steps that you plan to run in this action.
  steps: Listing<Step>
}

class Step {
  /// The command you want to run. This can be inline or a script in your action repository.
  run: String?

  /// The shell where you want to run the command.
  shell: ("bash" | "pwsh" | "python" | "sh" | "cmd" | "powershell" | String)?

  /// Selects an action to run as part of a step in your job.
  uses: String?

  /// A map of the input parameters defined by the action.
  ///
  /// Each input parameter is a key/value pair.
  /// Input parameters are set as environment variables.
  /// The variable is prefixed with INPUT_ and converted to upper case.
  with: Mapping<String, String | Number | Boolean>?

  /// The name of the composite run step.
  name: String?

  /// A unique identifier for the step.
  ///
  /// You can use the `id` to reference the step in contexts.
  id: String?

  /// You can use the if conditional to prevent a step from running unless a condition is met.
  ///
  /// You can use any supported context and expression to create a conditional.
  /// Expressions in an if conditional do not require the ${{ }} syntax.
  /// For more information, see
  /// https://help.github.com/en/articles/contexts-and-expression-syntax-for-github-actions.
  `if`: String?

  /// Sets a map of environment variables for only that step.
  env: (*Mapping<String, String | Number | Boolean> | String)?

  /// Prevents a job from failing when a step fails.
  ///
  /// Set to true to allow a job to pass when this step fails.
  /// Default if undefined: `false`
  `continue-on-error`: (Boolean | String)?

  /// Specifies the working directory where the command is run.
  `working-directory`: String?
}

/// Configures the image used for the Docker action.
class RunsDocker {
  /// You must set this value to 'docker'.
  using: "docker"

  /// The Docker image to use as the container to run the action.
  ///
  /// The value can be the Docker base image name, a local `Dockerfile` in your repository, or a
  /// public image in Docker Hub or another registry.
  /// To reference a `Dockerfile` local to your repository, use a path relative to your action
  /// metadata file.
  /// The `docker` application will execute this file.
  image: String

  /// Specifies a key/value map of environment variables to set in the container environment.
  env: (*Mapping<String, String | Number | Boolean> | String)?

  /// Overrides the Docker `ENTRYPOINT` in the `Dockerfile`, or sets it if one wasn't already
  /// specified.
  ///
  /// Use `entrypoint` when the `Dockerfile` does not specify an `ENTRYPOINT` or you want to
  /// override the `ENTRYPOINT` instruction.
  /// If you omit `entrypoint`, the commands you specify in the Docker `ENTRYPOINT` instruction will
  /// execute.
  /// The Docker `ENTRYPOINT instruction has a *shell* form and *exec* form.
  /// The Docker `ENTRYPOINT` documentation recommends using the *exec* form of the `ENTRYPOINT`
  /// instruction.
  entrypoint: String?

  /// Allows you to run a script before the `entrypoint` action begins.
  ///
  /// For example, you can use `pre-entrypoint:` to run a prerequisite setup script.
  /// GitHub Actions uses `docker run` to launch this action, and runs the script inside a new
  /// container that uses the same base image.
  /// This means that the runtime state is different from the main `entrypoint` container, and any
  /// states you require must be accessed in either the workspace, `HOME`, or as a `STATE_`
  /// variable.
  /// The `pre-entrypoint:` action always runs by default but you can override this using `pre-if`.
  `pre-entrypoint`: String?

  /// Allows you to run a cleanup script once the `runs.entrypoint` action has completed.
  ///
  /// GitHub Actions uses `docker run` to launch this action.
  /// Because GitHub Actions runs the script inside a new container using the same base image, the
  /// runtime state is different from the main `entrypoint` container.
  /// You can access any state you need in either the workspace, `HOME`, or as a `STATE_` variable.
  /// The `post-entrypoint:` action always runs by default but you can override this using
  /// `post-if`.
  `post-entrypoint`: String?

  /// An array of strings that define the inputs for a Docker container.
  ///
  /// Inputs can include hardcoded strings.
  /// GitHub passes the `args` to the container's `ENTRYPOINT` when the container starts up.
  /// The `args` are used in place of the `CMD` instruction in a `Dockerfile`.
  /// If you use `CMD` in your `Dockerfile`, use the guidelines ordered by preference: - Document
  /// required arguments in the action's README and omit them from the `CMD` instruction.
  /// - Use defaults that allow using the action without specifying any `args`.
  /// - If the action exposes a `--help` flag, or something similar, use that to make your action
  /// self-documenting.
  args: Listing<String>?
}

output {
  renderer = new YamlRenderer {}
}
