//===----------------------------------------------------------------------===//
// Copyright Â© 2024 Apple Inc. and the Pkl project authors. All rights reserved.
//
// Licensed under the Apache License, Version 2.0 (the "License");
// you may not use this file except in compliance with the License.
// You may obtain a copy of the License at
//
//     https://www.apache.org/licenses/LICENSE-2.0
//
// Unless required by applicable law or agreed to in writing, software
// distributed under the License is distributed on an "AS IS" BASIS,
// WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
// See the License for the specific language governing permissions and
// limitations under the License.
//===----------------------------------------------------------------------===//
/// This example uses steps from the [circleci/node](https://circleci.com/developer/orbs/orb/circleci/node) orb.
///
/// It defins local classes that extend [AbstractStep], so that step definitions are type safe.
amends "../Config.pkl"

/// Install custom versions of Node.js, and optionally NPM/Yarn, in any
/// execution environment (Docker/Linux, macOS, machine) that does not have
/// it preinstalled.
local class NodeInstall extends AbstractStep {
  fixed hidden __name__ = "node/install"

  /// Install Yarn?
  ///
  /// Default: `false`
  `install-yarn`: Boolean?

  /// Where should Node.js be installed?
  ///
  /// Default: `/usr/local`
  `node-install-dir`: String?

  /// Specify the full version tag to install.
  ///
  /// To install the latest version, set the version to `latest`.
  /// If unspecified, the version listed in .nvmrc will be installed.
  /// If no .nvmrc file exists the active LTS version of Node.js will be installed by default.
  /// For a full list of releases, see the following: <https://nodejs.org/en/download/releases>
  `node-version`: String?

  /// Pick a version of Yarn to install.
  ///
  /// If no version is specified, the latest stable version will be installed: <https://github.com/yarnpkg/yarn/releases>
  `yarn-version`: String?
}

jobs {
  ["test"] {
    machine {
      image = "ubuntu-2004:current"
    }
    steps {
      "checkout"
      new NodeInstall { `node-version` = "v20.11.1" }
      new RunStep { command = "npm install" }
      new RunStep { command = "npm test" }
    }
  }
}
