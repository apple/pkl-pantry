//===----------------------------------------------------------------------===//
// Copyright Â© 2024 Apple Inc. and the Pkl project authors. All rights reserved.
//
// Licensed under the Apache License, Version 2.0 (the "License");
// you may not use this file except in compliance with the License.
// You may obtain a copy of the License at
//
//     https://www.apache.org/licenses/LICENSE-2.0
//
// Unless required by applicable law or agreed to in writing, software
// distributed under the License is distributed on an "AS IS" BASIS,
// WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
// See the License for the specific language governing permissions and
// limitations under the License.
//===----------------------------------------------------------------------===//
module org.openapis.v3_0.examples.SwallowSchema

import "../SchemaGenerator.pkl"

/// A friend of a Swallow.
class SwallowFriend {
  /// The name of the friend.
  name: String

  /// Is this friend also a Swallow?
  isSwallow: Boolean

  /// Any extra attributes for the friend.
  tags: Mapping<String, String>
}

/// This is a Swallow
class Swallow {

  /// Does this Swallow have a gulp?
  hasGulp: Boolean

  /// One of the variants for this swallow.
  variants: Listing<"African"|"European">

  /// What is the air speed of this swallow?
  airSpeed: Number?

  /// Is this Swallow a Duck?
  ///
  /// This is deprecated because no swallows are ever ducks.
  @Deprecated
  isDuck: Boolean?

  /// How old is this Swallow?
  age: UInt8

  /// What are the nicknames?
  nicknames: Listing<String>

  /// Who is the best friend of this Swallow?
  bestFriend: SwallowFriend

  /// Who are the friends of this Swallow?
  friends: Listing<SwallowFriend>

  /// Any extra attributes.
  tags: Mapping<String, String>

  /// The typical size of a flight path of this Swallow.
  typicalFlightPathSize: DataSize

  /// This is a list. It really should not be used, but sometimes people make mistakes. Regardless,
  /// it should turn into an array type.
  list: List<String>

  /// Swoop!
  ///
  /// This should not appear in the schema output.
  swoop: (String) -> String = (input) -> "SWOOP \(input)"

  /// Swoopwoop!
  ///
  /// This should also not appear in the schema output.
  function swoopwoop() = "swoopwoop!"

  /// soar!
  ///
  /// Don't show me in the schema because I don't show up in any rendered output.
  hidden soar: Boolean?
}

local generator = new SchemaGenerator {
  converters {
    [DataSize] {
      type = "string"
      description = "A data size description"
    }
  }
}

output = generator.generate(Swallow).output
