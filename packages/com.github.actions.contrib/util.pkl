//===----------------------------------------------------------------------===//
// Copyright Â© 2025 Apple Inc. and the Pkl project authors. All rights reserved.
//
// Licensed under the Apache License, Version 2.0 (the "License");
// you may not use this file except in compliance with the License.
// You may obtain a copy of the License at
//
//     https://www.apache.org/licenses/LICENSE-2.0
//
// Unless required by applicable law or agreed to in writing, software
// distributed under the License is distributed on an "AS IS" BASIS,
// WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
// See the License for the specific language governing permissions and
// limitations under the License.
//===----------------------------------------------------------------------===//
module com.github.actions.contrib.util

import "@deepToTyped/deepToTyped.pkl"
import "@io.github.typesafegithub/ActionTypes.pkl"

function toActionTypes(raw: Mapping): ActionTypes =
  let (_inputs: Mapping? = raw.getOrNull("inputs"))
  let (_outputs: Mapping? = raw.getOrNull("outputs"))
    new {
      when (_inputs != null) {
        inputs {
          for (name, prop in _inputs) {
            [name] = toProperty(prop)
          }
        }
      }
      when (_outputs != null) {
        outputs {
          for (name, prop in _outputs) {
            [name] = toProperty(prop)
          }
        }
      }
    }

local function toProperty(raw: Mapping): ActionTypes.Property =
  deepToTyped.apply(ActionTypes.Property, raw) as ActionTypes.Property

/// Renders the string in pascal case.
///
/// Underscores, dashes and dots are all considered word separators.
///
/// Facts:
/// ```
/// pascalCase("foo-bar") == "FooBar"
/// pascalCase("foo.bar") == "FooBar"
/// pascalCase("foo_bar") == "FooBar"
/// pascalCase("fooBar") == "FooBar"
/// ```
function pascalCase(text: String) =
  let (words = text.split(Regex(#"[_.-]"#)))
    words.map((w) -> w.capitalize()).join("")
