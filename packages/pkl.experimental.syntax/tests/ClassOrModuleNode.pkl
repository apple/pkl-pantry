//===----------------------------------------------------------------------===//
// Copyright Â© 2024 Apple Inc. and the Pkl project authors. All rights reserved.
//
// Licensed under the Apache License, Version 2.0 (the "License");
// you may not use this file except in compliance with the License.
// You may obtain a copy of the License at
//
//     https://www.apache.org/licenses/LICENSE-2.0
//
// Unless required by applicable law or agreed to in writing, software
// distributed under the License is distributed on an "AS IS" BASIS,
// WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
// See the License for the specific language governing permissions and
// limitations under the License.
//===----------------------------------------------------------------------===//
module pkl.experimental.syntax.tests.ClassOrModuleNode

amends "pkl:test"

import "../ClassOrModuleNode.pkl"
import "../TypeNode.pkl"

facts {
  ["property definitions"] {
    new ClassOrModuleNode.PropertyDefinitionNode {
      name {
        value = "myProperty"
      }
      typeAnnotation {
        type = new TypeNode.StringLiteralTypeNode {
          value = "my string"
        }
      }
    }.render("") == """
      myProperty: "my string"
      """
  }
  ["property definitions - annotations"] {
    local prop = new ClassOrModuleNode.PropertyDefinitionNode {
      name {
        value = "myProperty"
      }
      typeAnnotation {
        type = new TypeNode.DeclaredTypeNode {
          name {
            parts {
              new { value = "MyType" }
            }
          }
        }
      }
      docComment {
        value = """
          This has been deprecated

          because of stuff.
          """
      }
      annotations {
        new {
          identifier {
            parts {
              new { value = "Deprecated" }
            }
          }
        }
      }
    }
    prop.render("") == """
      /// This has been deprecated
      ///
      /// because of stuff.
      @Deprecated
      myProperty: MyType
      """
    prop.render("  ") == """
        /// This has been deprecated
        ///
        /// because of stuff.
        @Deprecated
        myProperty: MyType
      """
  }
}
